#! zsh -f
#
# Install this function by placing it in your FPATH and then
# adding to your .zshrc the line if you use run-help function:
#       autoload -Uz run-help-ip

emulate -L zsh

local -a null

zparseopts -D -a null -- \
    V Version -Version \
    b: batch: -batch: \
    force -force \
    s stats -stats statistics -statistics \
    l: loops: -loops: \
    f: family: -family: \
    4 6 B D I O \
    o oneline -oneline \
    r resolve -resolve

if [[ $# -eq 0 ]]; then
    man ip
    return
fi

if [[ $1 == -* ]]; then
    man ip
    print -Plu2 "" "Unknown option '%B$1%b' for ip"
    return 1
fi

case $1 in
    (addr*) man ip-address ;;
    (addrlabel) man ip-addrlabel ;;
    (l2*) man ip-l2tp ;;
    (li*) man ip-link ;;
    (ma*) man ip-maddress ;;
    (mo*) man ip-monitor ;;
    (mr*) man ip-mroute ;;
    (nei*) man ip-neighbour ;;
    (netc*) man ip-netconf ;;
    (netn*) man ip-netns ;;
    (nt*) man ip-ntable ;;
    (ro*) man ip-route ;;
    (ru*) man ip-rule ;;
    (tcp*) man ip-tcp_metrics ;;
    (to*) man ip-token ;;
    (tu*) man ip-tunnel ;;
    (xf*) man ip-xfrm ;;
    (*) man ip ;;
esac

return $?

### That's all, folks!
