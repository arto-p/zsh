#! zsh -f
#
# Install this function by placing it in your FPATH and then
# adding to your .zshrc the line if you use run-help function:
#       autoload -Uz run-help-ip

emulate -L zsh

local null

zparseopts -D \
    V=null Version=null -Version=null \
    b:=null batch:=null -batch:=null \
    force=null -force=null \
    s=null stats=null -stats=null statistics=null -statistics=null \
    l:=null loops:=null -loops:=null \
    f:=null family:=null -family:=null \
    4=null 6=null B=null D=null I=null O=null \
    o=null oneline=null -oneline=null \
    r=null resolve=null -resolve=null

if [[ $# -eq 0 ]]; then
    man ip
    return
fi

if [[ $1 == -* ]]; then
    man ip
    print -Plu2 "" "Unknown option '%B$1%b' for ip"
    return 1
fi

case $1 in
    (addr*) man ip-address ;;
    (addrlabel) man ip-addrlabel ;;
    (l2*) man ip-l2tp ;;
    (li*) man ip-link ;;
    (ma*) man ip-maddress ;;
    (mo*) man ip-monitor ;;
    (mr*) man ip-mroute ;;
    (nei*) man ip-neighbour ;;
    (netc*) man ip-netconf ;;
    (netn*) man ip-netns ;;
    (nt*) man ip-ntable ;;
    (ro*) man ip-route ;;
    (ru*) man ip-rule ;;
    (tcp*) man ip-tcp_metrics ;;
    (to*) man ip-token ;;
    (tu*) man ip-tunnel ;;
    (xf*) man ip-xfrm ;;
    (*) man ip ;;
esac

return $?
