#! zsh -f
#
# Install this function by placing it in your FPATH and then
# adding to your .zshrc the line if you use run-help function:
#       autoload -Uz run-help-xargs

emulate -L zsh

local -a null
local -a zpargs

if [[ $OSTYPE == freebsd* ]]; then
    zpargs=( "o" "E:" "I:" "J:" "L:" \
             "n:" "o" "P:" "p" "r" "R:" \
             "S:" "s:" "t" "x" )
else
    # By default assume gnu utils
    zpargs=( "0" "-null" \
             "a:" "-arg-file:" \
             "d:" "-delimiter:" \
             "E:" "e::" "-eof::" \
             "I:" "i::" "-replace::" \
             "L:" "l::" "-max-lines::" \
             "n:" "-max-args:" \
             "P:" "-max-procs:" \
             "p" "-inetractive" \
             "-process-slot-var:" \
             "r" "-no-run-if-empty" \
             "s:" "-max-chars:" \
             "-show-limits" \
             "t" "-verbose" \
             "x" "-exit" \
             "-help" "-version" )
fi

zparseopts -D -a null -- $zpargs

if [[ $# -eq 0 ]]; then
    man xargs
    return $?
fi

if [[ $1 == -* ]]; then
    man xargs
    print -lPu2 "" "Unrecognized option '%B$1%b' for xargs"
    return 1
fi

man $1

return $?

### That's all, folks!
