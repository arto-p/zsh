#! zsh -f
#
# Install this function by placing it in your FPATH and then
# adding to your .zshrc the line if you use run-help function:
#       autoload -Uz run-help-xargs

emulate -L zsh

local null
typeset -a zpargs

if [[ $OSTYPE == freebsd* ]]; then
    zpargs=( "o=null" "E:=null" "I:=null" "J:=null" "L:=null" \
	"n:=null" "o=null" "P:=null" "p=null" "r=null" "R:=null" \
	"S:=null" "s:=null" "t=null" "x=null" )
else
    # By default assume gnu utils
    zpargs=( "0=null" "-null=null" \
	"a:=null" "-arg-file:=null" \
	"d:=null" "-delimiter:=null" \
	"E:=null" "e::=null" "-eof::=null" \
	"I:=null" "i::=null" "-replace::=null" \
	"L:=null" "l::=null" "-max-lines::=null" \
	"n:=null" "-max-args:=null" \
	"P:=null" "-max-procs:=null" \
	"p=null" "-inetractive=null" \
	"-process-slot-var:=null" \
	"r=null" "-no-run-if-empty=null" \
	"s:=null" "-max-chars:=null" \
	"-show-limits=null" \
	"t=null" "-verbose=null" \
	"x=null" "-exit=null" \
	"-help=null" "-version=null" )
fi

zparseopts -D $zpargs

if [[ $# -eq 0 ]]; then
    man xargs
    return
fi

if [[ $1 == -* ]]; then
    man xargs
    print -lPu2 "" "Unrecognized option '%B$1%b' for xargs"
    return 1
fi

man $1

return $?

